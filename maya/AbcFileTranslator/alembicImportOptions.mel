

global proc alembicImportOptions_modeChanged()
{
   string $val = `optionMenuGrp -q -v optMode`;
   
   checkBoxGrp -e -ed ($val == "Update") optAddNode;
   checkBoxGrp -e -ed ($val == "Update") optRemNode;
   checkBoxGrp -e -ed ($val == "Add") optReparent;
}

global proc int alembicImportOptions(string $parent,
                                     string $action,
                                     string $initialSettings,
                                     string $resultCallback)
{
   global string $gAbcLastOptions;

   int $result = 0;
   string $flt = "";
   string $impOpts = "";
   string $expOpts = "";

   string $curParent = `setParent -query`;
   setParent $parent;

   if ($action == "post")
   {
      // Build UI elements     
      frameLayout -collapsable false -label "Contents" -bv false;
         columnLayout;
            optionMenuGrp -l "Mode:" -adj 2 -cal 1 right -cal 2 left -cc "alembicImportOptions_modeChanged" optMode;
            menuItem -l "Add";
            menuItem -l "Update";
            menuItem -l "Replace";
            optionMenuGrp -e -v "Add" optMode;
            checkBoxGrp -ncb 1 -l "" -l1 "Add Missing Nodes To Scene" -adj 2 -cal 1 right -cal 2 left -v1 false optAddNode;
            checkBoxGrp -ncb 1 -l "" -l1 "Remove Missing Nodes From File" -adj 2 -cal 1 right -cal 2 left -v1 false optRemNode;
            checkBoxGrp -ncb 1 -l "" -l1 "Reparent To Selection" -adj 2 -cal 1 right -cal 2 left -v1 false optReparent;
            checkBoxGrp -ncb 1 -l "" -l1 "Create Instances" -adj 2 -cal 1 right -cal 2 left -v1 false optCreateInst;
         setParent ..;
      setParent ..;
      
      frameLayout -collapsable false -label "Mesh" -bv false;
         columnLayout;
            checkBoxGrp -ncb 1 -l "" -l1 "Recreate All Color Sets" -adj 2 -cal 1 right -cal 2 left -v1 false optColSets;
            checkBoxGrp -ncb 1 -l "" -l1 "Recreate All UV Sets" -adj 2 -cal 1 right -cal 2 left -v1 false optUVSets;
            checkBoxGrp -ncb 1 -l "" -l1 "Read Normals" -adj 2 -cal 1 right -cal 2 left -v1 false optReadNormals;
            checkBoxGrp -ncb 1 -l "" -l1 "Create Reference Object" -adj 2 -cal 1 right -cal 2 left -v1 false optRefObj;
         setParent ..;
      setParent ..;
      
      frameLayout -collapsable false -label "Time" -bv false;
         columnLayout;
            checkBoxGrp -ncb 1 -l "" -l1 "Fit Time Range" -adj 2 -cal 1 right -cal 2 left -v1 false optFitTime;
            checkBoxGrp -ncb 1 -l "" -l1 "Set To Start Frame" -adj 2 -cal 1 right -cal 2 left -v1 false optStartFrame;
         setParent ..;
      setParent ..;
      
      frameLayout -collapsable false -label "Filters" -bv false;
         columnLayout;
            textFieldGrp -l "Include Objects:" -adj 2 -cal 1 right -cal 2 left -tx "" optFlt;
            textFieldGrp -l "Exclude Objects:" -adj 2 -cal 1 right -cal 2 left -tx "" optExcFlt;
         setParent ..;
      setParent ..;
      
      separator -style "in";
      
      checkBoxGrp -ncb 1 -l "" -l1 "Debug" -adj 2 -cal 1 right -cal 2 left -v1 false optDbg;
      
      if (size($initialSettings) > 0)
      {
         // parse option string and set UI
         string $optlist[];
         int $n = tokenize($initialSettings, ";", $optlist);
         string $options = "";
         
         for ($i=0; $i<$n; $i++)
         {
            string $keyval[];
            int $n2 = tokenize($optlist[$i], "=", $keyval);
            
            if ($n2 < 2)
            {
               continue;
            }
            
            string $key = $keyval[0];
            string $val = $keyval[1];
            for ($j=2; $j<$n2; $j++)
            {
               $val += "=";
               $val += $keyval[$j];
            }
            
            if (size(match($key+"=", $options)) > 0)
            {
               // options already processed
               continue;
            }
            else
            {
               if (size($options) > 0)
               {
                  $options += ";";
               }
               $options += $optlist[$i];
            }
            
            if ($key == "ftr")
            {
               checkBoxGrp -e -v1 ($val == "1") optFitTime;
            }
            else if ($key == "rcs")
            {
               checkBoxGrp -e -v1 ($val == "1") optColSets;
            }
            else if ($key == "rus")
            {
               checkBoxGrp -e -v1 ($val == "1") optUVSets;
            }
            else if ($key == "rmn")
            {
               checkBoxGrp -e -v1 ($val == "1") optReadNormals;
            }
            else if ($key == "crm")
            {
               checkBoxGrp -e -v1 ($val == "1") optRefObj;
            }
            else if ($key == "d")
            {
               checkBoxGrp -e -v1 ($val == "1") optDbg;
            }
            else if ($key == "ft")
            {
               textFieldGrp -e -tx $val optFlt;
            }
            else if ($key == "eft")
            {
               textFieldGrp -e -tx $val optExcFlt;
            }
            else if ($key == "sts")
            {
               checkBoxGrp -e -v1 ($val == "1") optStartFrame;
            }
            else if ($key == "mode")
            {
               optionMenuGrp -e -v $val optMode;
            }
            else if ($key == "rpr")
            {
               checkBoxGrp -e -v1 ($val == "1") optReparent;
            }
            else if ($key == "crt")
            {
               checkBoxGrp -e -v1 ($val == "1") optAddNode;
            }
            else if ($key == "rm")
            {
               checkBoxGrp -e -v1 ($val == "1") optRemNode;
            }
            else if ($key == "ci")
            {
               checkBoxGrp -e -v1 ($val == "1") optCreateInst;
            }
         }

         // at this point, $options is a 'clean' version of $initialSettings
         $initialSettings = $options;
      }
      
      // Update editable state
      alembicImportOptions_modeChanged();

      $gAbcLastOptions = $initialSettings;

      $result = 1;
   }
   else if ($action == "query")
   {
      // Build option string
      $mode = `optionMenuGrp -q -v optMode`;
      
      $impOpts = "mode=" + $mode;
      
      if ($mode == "Update")
      {
         if (`checkBoxGrp -q -v1 optAddNode` == true)
         {
            $impOpts += ";crt=1";
         }
         if (`checkBoxGrp -q -v1 optRemNode` == true)
         {
            $impOpts += ";rm=1";
         }
      }
      else if ($mode == "Add")
      {
         if (`checkBoxGrp -q -v1 optReparent` == true)
         {
            $impOpts += ";rpr=1";
         }
      }
      
      if (`checkBoxGrp -q -v1 optFitTime` == true)
      {
         $impOpts += ";ftr=1";
      }
      
      if (`checkBoxGrp -q -v1 optStartFrame` == true)
      {
         $impOpts += ";sts=1";
      }
      
      if (`checkBoxGrp -q -v1 optColSets` == true)
      {
         $impOpts += ";rcs=1";
      }
      
      if (`checkBoxGrp -q -v1 optUVSets` == true)
      {
         $impOpts += ";rus=1";
      }
      
      if (`checkBoxGrp -q -v1 optReadNormals` == true)
      {
         $impOpts += ";rmn=1";
      }
      
      if (`checkBoxGrp -q -v1 optRefObj` == true)
      {
         $impOpts += ";crm=1";
      }
      
      $flt = `textFieldGrp -q -tx optFlt`;
      if (size($flt) > 0)
      {
         $impOpts += ";ft=" + $flt + "";
      }
      
      $flt = `textFieldGrp -q -tx optExcFlt`;
      if (size($flt) > 0)
      {
         $impOpts += ";eft=" + $flt + "";
      }
      
      if (`checkBoxGrp -q -v1 optDbg` == true)
      {
         $impOpts += ";d=1";
      }
      
      if (`checkBoxGrp -q -v1 optCreateInst` == true)
      {
         $impOpts += ";ci=1";
      }

      // Preserve other options in $initialSettings
      if (size($initialSettings) > 0)
      {
         // parse option string and set UI
         string $optlist[];
         int $n = tokenize($initialSettings, ";", $optlist);
         
         string $frt;
         float $sf, $ef;
         
         for ($i=0; $i<$n; $i++)
         {
            string $keyval[];
            
            int $n2 = tokenize($optlist[$i], "=", $keyval);
            
            if ($n2 < 2)
            {
               if (size($expOpts) > 0)
               {
                  $expOpts += ";";
               }
               $expOpts += $optlist[$i];
               continue;
            }

            string $key = $keyval[0];
            string $val = $keyval[1];
            for ($j=2; $j<$n2; $j++)
            {
               $val += "=";
               $val += $keyval[$j];
            }
            
            if ($key == "ftr" ||
                $key == "rcs" ||
                $key == "rus" ||
                $key == "rmn" ||
                $key == "crm" ||
                $key == "d" ||
                $key == "ft" ||
                $key == "eft" ||
                $key == "sts" ||
                $key == "mode" ||
                $key == "rpr" ||
                $key == "crt" ||
                $key == "rm" ||
                $key == "ci")
            {
               continue;
            }
            else
            {
               if (size(match($key+"=", $expOpts)) > 0)
               {
                  // Import option is already set
                  continue;
               }
               if (size($expOpts) > 0)
               {
                  $expOpts += ";";
               }
               $expOpts += $key + "=" + $val;
            }
         }
      }

      string $allOpts = "";
      if (size($impOpts))
      {
         $allOpts += $impOpts;
      }
      if (size($expOpts))
      {
         if (size($allOpts) > 0)
         {
            $allOpts += ";";
         }
         $allOpts += $expOpts;
      }

      $gAbcLastOptions = $allOpts;

      eval($resultCallback + " \"" + $gAbcLastOptions + "\""); 

      $result = 1;
   }
   else
   {
      $gAbcLastOptions = $initialSettings;

      $result = 0;
   }

   setParent $curParent;

   return $result;
}
